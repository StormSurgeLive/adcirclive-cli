#!/usr/bin/env perl
package bin::adcirclive;

use v5.10;
use strict;
use warnings;
use FindBin qw/$Bin/;
use Util::H2O::More qw/ddd d2o h2o ini2h2o o2d o2h Getopt2h2o HTTPTiny2h2o/;
use HTTP::Tiny qw//;
use Digest::SHA qw/sha256_hex/;
use MIME::Base64 qw/encode_base64/;
use Time::HiRes qw//;
use JSON::PP qw/decode_json encode_json/;
use Dispatch::Fu;

our $VERSION = q{1.0};

use constant {
    EXIT_SUCCESS   => 0,
    EXIT_ERROR     => 255,
    DEFAULT_CONFIG => sprintf( qq{%s/asgs-global.conf}, ( getpwuid($<) )[7] ),    # $HOME/asgs-global.conf
    BASEURL        => q{https://tools.adcirc.live},
    true           => $JSON::PP::true,
    false          => $JSON::PP::false,
};

my $config     = ini2h2o DEFAULT_CONFIG;
my $subcommand = shift @ARGV;

#<<<
exit dispatch {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  xdefault $subcmd, q{help};
} [ $subcommand, \@ARGV, $config ],
 on config => \&get_asgs_config,
 on help   => \&show_help,
 on meshes => \&get_meshes,
 on uuid   => \&get_uuid,
 on xdmf   => \&get_xdmf,
 on xdmftv => \&get_xdmf_tv,
;
#>>>

sub _get_signed_ua($) {
    my ($config) = @_;
    my $key    = $config->adcirclive->apikey;
    my $secret = $config->adcirclive->apisecret;

    # add $secret to string, get SHA256 hash
    my $nonce          = Time::HiRes::time * 100_000;
    my $signature      = sha256_hex( $nonce . $secret );
    my $_authorization = sprintf( qq{%s:%s}, $key, $signature );
    my $authorization  = encode_base64( $_authorization, q{} );

    my $default_headers = {
        q{x-adcirclive-api-version} => q{1.0},
        q{x-auth-nonce}             => $nonce,
        q{Authorization}            => $authorization,
        q{Content-Type}             => q{application/json},
    };

    return HTTP::Tiny->new( ( default_headers => $default_headers ) );
}

sub _lookup_mesh {
  my $name = shift;
  # get mesh look up table
  my $ua       = _get_signed_ua $config;
  my $URL      = sprintf qq{%s/api/meshes}, BASEURL;
  my $mesh_table = h2o $ua->get($URL);
  my $mesh;

  if ($mesh_table->success) {
    my $meshes = d2o decode_json $mesh_table->content;
    my %meshes = map { $_->name => $_ } $meshes->all;
    $mesh      = $meshes{$name};
  }

  return $mesh;
}

#
# CLI interface for ASGS Config,
#
# minimal:
#
#   adcirclive asgsconfig --operator be --asgsadmin asgs-notify@memenesia.net --met_kind ATCF --gridname HSOFS --ncpu 959
#
# optional flags:
#   --emailnotify  # overrides $config->asgslive->OPENDAPNOTIFY if in ~/asgs-global.conf
#   --instancename # defaults to ASGS standard format
#   --machine      # defaults to Linux # check, used here just to create default INSTANCENAME
#   --ppn          # defaults to NULL  # check
#   --numwriters   # defaults to 1     # check
#   --storm        # required with "--metkind ATCF", ignored for others
#   --year         # defaults to current year, only used with "--metkid ATCF"
#
# interactive (second pass?):
#
#   adcirclive asgsconfig -i    # ... asks for minimal flags ^^, if more needed refers to the SPA
#
# defaults available for setting in in ~/asgs-global.conf:
#
#   [adcirclive]
#   ASGSADMIN=...               $config->asgslive->ASGSADMIN
#   OPENDAPNOTIFY=...           $config->asgslive->OPENDAPNOTIFY
#
# TODO: embed defaults.json in this script
#
sub get_asgs_config {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;

  my $defaults = d2o -autoundef, decode_json do { local $/; open my $FH, "<", "$Bin/../share/defaults.json"; <$FH> };

  # options that are also in ASGS config
  my @options   = qw/account adcircversion asgsadmin basin coldstartdate cycle cycletimelimit default emailnotify enablepoststatus enablestatusnotify
                  finish_nowcast_scenario gridname handle hindcastlength hotorcold instancename intendedaudience lastsubdir machine mesh met_kind ncpu ncpucapacity
                  notify_script numwriters opendapnotify opendappost output postprocess ppn qos queuesys reinitializeswan specifycoldstart statusnotify storm tds
                  tidefac varflux waves year/;

  # cli options that are not in ASGS config
  my @extraopts = qw/operator/;

  # cli flags are: @options + @extraopts
  my @flags = map { "$_=s" } @options, @extraopts;

  my $o       = Getopt2h2o $ARGV_ref, { as => "stdin", met_kind => "NAM" }, @flags;
  $o          = d2o -autoundef, o2d $o; # hackaround missing optional hash keys,

#TODO - update "Util::H2O::More::Getopt2h2o" to accept "-autoundef"

  my @missing = ();
  foreach my $required (qw/operator asgsadmin met_kind gridname ncpu/) {
    push @missing, "--".$required if (not $o->$required);
  }
  if (@missing) {
    my $msg = sprintf "FATAL - Missing required option flags: %s\n", join(", ",@missing);
    die $msg;
  }

  my $kind     = uc $o->met_kind;

  my $payload  = $defaults->$kind; # get default entry, update/modify based on supplied options

  foreach my $field (@options) {
    if ($o->$field) {
      my $FIELD = uc $field;
      $payload->$FIELD($o->$field);
    }
  }

  my $ua       = _get_signed_ua $config;
  my $URL      = sprintf qq{%s/spa/models/ASGS/api/file/config}, BASEURL;

  $payload->INSTANCENAME($o->instancename // sprintf("%s_%s_%s_%s", $o->gridname // "HSOFS", $o->met_kind // "NAM", $o->machine // "Linux", $o->operator // 'ukwn'));

  my $response = HTTPTiny2h2o $ua->post($URL, { content => encode_json o2d  $payload });

  print $response->content->content;
}

sub get_xdmf_tv {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  my $o        = Getopt2h2o $ARGV_ref, {as => q{stdout}, fort63 => 1}, qw/adcirc=s as=s mesh=s fort63 fort64 fort73 fort74 rads64 swanhs63 swantps63 swandir63 paraview=s coldstart=s outputstart=s count=i timeinc=i/;
  my $ua       = _get_signed_ua $config;

 my $mesh = _lookup_mesh($o->mesh);

 #"2024-01-30T06:00:00.000Z";
 my $now  = POSIX::strftime("%Y-%m-%dT%H-%M-%SZ",localtime(time));

 my $content = encode_json {
    mesh => {
      name     => $o->mesh,
      nodes    => $mesh->nodes,
      elements => $mesh->elements,
    },
    output => {
      fort63    => ($o->fort63)    ? true : false, # encode_json needs $JSON::PP's booleans here
      fort64    => ($o->fort64)    ? true : false, # hence the use of bareword 'true' and 'false'.
      fort73    => ($o->fort73)    ? true : false, # see them defined at the top of this file
      fort74    => ($o->fort74)    ? true : false,
      rads64    => ($o->rads64)    ? true : false,
      swanDIR63 => ($o->swandir63) ? true : false,
      swanHS63  => ($o->swanhs63)  ? true : false,
      swanTPS63 => ($o->swantps63) ? true : false,
      time => {
        coldStartDateTime   => $o->coldstart   // $now, 
        outputStartDateTime => $o->outputstart // $now, 
        numDataSets         => $o->count       // 120,
        timeIncrement       => $o->timeinc     // 3600,
      },
    },
    paraviewVersion => $o->paraview // q{unknown},
    adcircVersion   => $o->adcirc   // q{unknown},
  };

  my $URL      = sprintf qq{%s/spa/paraview/XDMF/api/file/timevarying}, BASEURL;
  my $response = h2o $ua->post($URL, { content => $content });

  if ($response->success) {
    my $file_info = d2o decode_json $response->content;
    if ($o->as eq q{stdout}) {
      print STDOUT $file_info->content;
    }
    else { #if ($o->as eq q{json}) {
      print $response->content;
    }
  }
  return EXIT_SUCCESS;
}

sub get_xdmf {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  my $o        = Getopt2h2o $ARGV_ref, {as => q{stdout}, maxele => 1}, qw/adcirc=s as=s mesh=s maxele maxwvel maxvel minpr maxrs swanhsmax swantpsmax paraview=s/;
  my $ua       = _get_signed_ua $config;

 my $mesh = _lookup_mesh($o->mesh);

 my $content = encode_json {
    mesh => {
      name     => $o->mesh,
      nodes    => $mesh->nodes,
      elements => $mesh->elements,
    },
    output => {
      maxEle     => ($o->maxele)     ? true : false, # bareword true/false defined at top of
      maxWVel    => ($o->maxwvel)    ? true : false, # file, required by JSON::PP::encode_json
      maxVel     => ($o->maxvel)     ? true : false,
      minPr      => ($o->minpr)      ? true : false,
      maxRS      => ($o->maxrs)      ? true : false,
      swanHSMax  => ($o->swanhsmax)  ? true : false,
      swanTPSMax => ($o->swantpsmax) ? true : false,
    },
    paraviewVersion => $o->paraview // q{unknown},
    adcircVersion   => $o->adcirc   // q{unknown},
  };

  my $URL      = sprintf qq{%s/spa/paraview/XDMF/api/file/static}, BASEURL;
  my $response = h2o $ua->post($URL, { content => $content });

  if ($response->success) {
    my $file_info = d2o decode_json $response->content;
    if ($o->as eq q{stdout}) {
      print STDOUT $file_info->content;
    }
    else { #if ($o->as eq q{json}) {
      print $response->content;
    }
  }
  return EXIT_SUCCESS;
}

sub get_meshes {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  my $o        = Getopt2h2o $ARGV_ref, {as => q{json}}, qw/as=s/;
  my $ua       = _get_signed_ua $config;
  my $URL      = sprintf qq{%s/api/meshes}, BASEURL;
  my $response = h2o $ua->get($URL);
  if ($response->success) {
    my $meshes = d2o decode_json $response->content;
    if ($o->as eq q{json}) {
      print $response->content;
    }
    else {
      foreach my $mesh ($meshes->all) {
        printf qq{%-17s(%-8s nodes, %-8s elements)\n}, $mesh->name, $mesh->nodes, $mesh->elements;
      } 
      printf qq{\ntip: use "--as json" to get list in JSON for more flexible processing\n}
    }
  }
  return EXIT_SUCCESS;
}

sub get_uuid {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  ddd $subcmd, $ARGV_ref, $config;
}

sub show_help {
  my ($subcmd, $ARGV_ref, $config) = xshift_and_deref @_;
  print STDERR <<EOF;
adcirclive - Commandline client for tools.adcirc.live

Subcommands:
  adcirclive meshes - list all supported meshes
EOF
};

1;
